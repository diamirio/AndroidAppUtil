apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def artifact = new Properties()
artifact.load(new FileInputStream("$project.name/deploy.settings"))

def localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

def projectGroupId = 'com.tailoredapps.androidutil'
def projectWebUrl = 'https://github.com/tailoredmedia/AndroidAppUtil'
def projectGitUrl = 'https://github.com/tailoredmedia/AndroidAppUtil.git'

group = projectGroupId
version = libraryVersion


install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId projectGroupId
            artifactId artifact.id

            name artifact.name
            description 'AndroidUtil is a set of utility classes for Android application development.'
            url projectWebUrl

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'tailoredmedia'
                    name 'Tailored Apps'
                    email 'office@tailored-apps.com'
                }
            }
            scm {
                connection projectGitUrl
                developerConnection projectGitUrl
                url projectWebUrl
            }
        }
    }
}

bintray {
    user = localProperties.getProperty("bintray.user")
    key = localProperties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'AndroidUtil'
        name = artifact.id
        desc = artifact.description
        websiteUrl = projectWebUrl
        vcsUrl = projectGitUrl
        licenses = ["Apache-2.0"]
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = artifact.description
        }
    }
}

if (project.hasProperty("kotlin")) {
    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc, dependsOn: dokka) {

    }
} else if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}